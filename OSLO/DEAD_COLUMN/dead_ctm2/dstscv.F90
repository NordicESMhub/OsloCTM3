! $Header: /mn/hox/ozon/ctm2_model/ctm2_src/dst_dst/dstscv.F90,v 1.1 2003/04/15 14:41:36 alfgr Exp $

! Purpose: Wet scavenging processes, development edition

! dstscv.F90 is based on, and is intended to one day replace, dstdpswet.F90
! dstscv.F90 implements wet deposition with size-dependent scavenging physics

! Usage:
! use dstscv ! [mdl] Aerosol scavenging properties

! Requires dst.h for DST_MSS_BDG
#include <dst.h> /* Dust preprocessor tokens */

module dstscv ! [mdl] Aerosol scavenging properties
  use precision ! [mdl] Precision r8, i8, ...
  use dstgrd,only:dst_nbr ! [mdl] Dust grid sizes
  implicit none
  save ! [stt] Changes to common variables are sticky
  
  ! Variables computed at run time, initialized in dst_psd_ini()
  real(r8) scv_cff_mss_avg_pcp_nrm_cnv(dst_nbr) ! [m2 kg-1] Mass mean scavenging coefficient, precipitation-normalized, convective
  real(r8) scv_cff_mss_avg_pcp_nrm_str(dst_nbr) ! [m2 kg-1] Mass mean scavenging coefficient, precipitation-normalized, stratiform
  real(r8) frc_ice_scv(dst_nbr) ! [frc] Fraction of ice cloud allowing nucleation scavenging
  real(r8) z_scv(dst_nbr) ! [(kg dust/kg rain)/(kg dust/kg air)] Scavenging ratio
  
contains
  
  subroutine dst_scv_cmn_ini()
    ! Purpose: Initialize wet scavenging parameters
    ! dst_scv_cmn_ini() is called by dst_msc_cmn_ini()
    ! Dependencies: Routine requires dmt_vwr, must be called after dst_psd_ini()
    use dstaer ! [mdl] Aerosol microphysical properties
    use dstgrd,only:dst_nbr ! [mdl] Dust grid sizes
    use precision ! [mdl] Precision r8, i8, ...
    use vec_mdl,only:ntp_vec ! [mdl] Vector manipulation, interpolation, rebinning
    use xtr_mdl ! [mdl] Extrapolation constants
    implicit none
    ! Parameters
    integer,parameter::xtr_typ_LHS=xtr_vrb ! [enm] LHS Extrapolation type
    integer,parameter::xtr_typ_RHS=xtr_vrb ! [enm] RHS Extrapolation type
    ! Data generated by mie on Mon Aug 28 16:44:17 2000
    ! mie version Unknown dated Unknown
    ! Saharan dust aerosol mass extinction coefficient in m2 kg-1
    ! Saharan dust aerosol scavenging coefficient, precipitation-normalized in m2 kg-1
#if 0 /* Some Fortran compilers choke on very long comments */
    ! Command line: mie --no_wrn_ntp -no_mie --hrz --dbg=1 --dst_lbl=01 --dsd_mnm=1.0 --dsd_mxm=4000.0 --dsd_nbr=200 --gsd_pcp_anl=1.86 --dmt_pcp_nma=1000.0 --flx_vlm_pcp=1.0e-6 --aer_typ=saharan_dust --dist=lognormal --sz_grd=log --sz_mnm=0.005 --sz_mxm=50.0 --sz_nbr=64 --rds_nma=0.2986 --gsd_anl=2.0 /data/zender/mie/out.nc
#endif /* not 0 */
    integer,parameter::dst_nbr_hrz=64 ! [nbr] Number of high resolution bins
    real(r8),dimension(dst_nbr_hrz),parameter::scv_cff_pcp_nrm_cnv_dst_hrz=(/  &
         1.5897641e-03, 1.3476803e-03, 1.1436866e-03, 9.7166962e-04,  &
         8.2651991e-04, 7.0396246e-04, 6.0041808e-04, 5.1288848e-04,  &
         4.3885942e-04, 3.7622152e-04, 3.2320418e-04, 2.7831987e-04,  &
         2.4031854e-04, 2.0814942e-04, 1.8092897e-04, 1.5791466e-04,  &
         1.3848314e-04, 1.2211206e-04, 1.0836589e-04, 9.6884069e-05,  &
         8.7372275e-05, 7.9595913e-05, 7.3375915e-05, 6.8586836e-05,  &
         6.5157270e-05, 6.3072272e-05, 6.2379047e-05, 6.3195432e-05,  &
         6.5722859e-05, 7.0264716e-05, 7.7251738e-05, 8.7277396e-05,  &
         1.0114563e-04, 1.1993577e-04, 1.4508948e-04, 1.7852767e-04,  &
         6.9217011e-04, 9.8793171e-03, 3.1971984e-02, 6.2812500e-02,  &
         9.9596232e-02, 1.3957511e-01, 1.8010621e-01, 2.1895881e-01,  &
         2.5454021e-01, 2.8595984e-01, 3.1295082e-01, 3.3571437e-01,  &
         3.5475212e-01, 3.7072664e-01, 3.8436484e-01, 3.9640611e-01,  &
         4.0758434e-01, 4.1863608e-01, 4.3032637e-01, 4.4358557e-01,  &
         4.5924133e-01, 4.7840023e-01, 5.0210440e-01, 5.3104508e-01,  &
         5.6500381e-01, 6.0031271e-01, 6.2540495e-01, 6.4466858e-01 /)
    
    ! Stratiform scavenging
#if 0 /* Some Fortran compilers choke on very long comments */
    ! Command line: mie --no_wrn_ntp -no_mie --hrz --dbg=1 --dst_lbl=01 --dsd_mnm=1.0 --dsd_mxm=4000.0 --dsd_nbr=200 --gsd_pcp_anl=1.86 --dmt_pcp_nma=400.0 --flx_vlm_pcp=0.277e-6 --aer_typ=saharan_dust --dist=lognormal --sz_grd=log --sz_mnm=0.005 --sz_mxm=50.0 --sz_nbr=64 --rds_nma=0.2986 --gsd_anl=2.0 /data/zender/mie/out.nc
#endif /* not 0 */
    real(r8),dimension(dst_nbr_hrz),parameter::scv_cff_pcp_nrm_str_dst_hrz=(/  &
         4.9361251e-03, 4.1806730e-03, 3.5449979e-03, 3.0096555e-03,  &
         2.5584465e-03, 2.1778606e-03, 1.8566155e-03, 1.5852820e-03,  &
         1.3559718e-03, 1.1620797e-03, 9.9807011e-04, 8.5930259e-04,  &
         7.4188155e-04, 6.4253848e-04, 5.5852794e-04, 4.8754705e-04,  &
         4.2766484e-04, 3.7726737e-04, 3.3501090e-04, 2.9978782e-04,  &
         2.7069784e-04, 2.4702997e-04, 2.2824891e-04, 2.1399201e-04,  &
         2.0407054e-04, 1.9848152e-04, 1.9742698e-04, 2.0134579e-04,  &
         2.1095932e-04, 2.2733609e-04, 2.5198181e-04, 2.8696252e-04,  &
         3.3507260e-04, 4.0006099e-04, 4.8693677e-04, 6.0237874e-04,  &
         1.6499147e-03, 2.0755932e-02, 6.3879907e-02, 1.2215281e-01,  &
         1.8957390e-01, 2.6075196e-01, 3.3104798e-01, 3.9697531e-01,  &
         4.5638159e-01, 5.0836754e-01, 5.5303204e-01, 5.9116012e-01,  &
         6.2395012e-01, 6.5281636e-01, 6.7928034e-01, 7.0493299e-01,  &
         7.3145419e-01, 7.6064801e-01, 7.9442638e-01, 8.3490026e-01,  &
         8.8266754e-01, 9.3715608e-01, 9.9514586e-01, 1.0515112e+00,  &
         1.1009588e+00, 1.1354960e+00, 1.1283506e+00, 1.1151069e+00 /)
    
    real(r8),dimension(dst_nbr_hrz),parameter::dmt_ctr_hrz=(/  &
         1.0773910e-08, 1.2441516e-08, 1.4367239e-08, 1.6591027e-08,  &
         1.9159019e-08, 2.2124489e-08, 2.5548960e-08, 2.9503479e-08,  &
         3.4070084e-08, 3.9343519e-08, 4.5433190e-08, 5.2465424e-08,  &
         6.0586132e-08, 6.9963768e-08, 8.0792901e-08, 9.3298183e-08,  &
         1.0773906e-07, 1.2441511e-07, 1.4367232e-07, 1.6591021e-07,  &
         1.9159012e-07, 2.2124479e-07, 2.5548951e-07, 2.9503465e-07,  &
         3.4070069e-07, 3.9343499e-07, 4.5433163e-07, 5.2465396e-07,  &
         6.0586092e-07, 6.9963721e-07, 8.0792842e-07, 9.3298115e-07,  &
         1.0773897e-06, 1.2441503e-06, 1.4367223e-06, 1.6591009e-06,  &
         1.9158997e-06, 2.2124464e-06, 2.5548932e-06, 2.9503444e-06,  &
         3.4070044e-06, 3.9343472e-06, 4.5433130e-06, 5.2465357e-06,  &
         6.0586044e-06, 6.9963671e-06, 8.0792788e-06, 9.3298058e-06,  &
         1.0773891e-05, 1.2441495e-05, 1.4367213e-05, 1.6590999e-05,  &
         1.9158986e-05, 2.2124450e-05, 2.5548916e-05, 2.9503426e-05,  &
         3.4070024e-05, 3.9343449e-05, 4.5433106e-05, 5.2465330e-05,  &
         6.0586011e-05, 6.9963629e-05, 8.0792743e-05, 9.3298113e-05 /)
    
    ! Precomputed values for common configurations
#if 0
    ncks -H -u -C -F -v scv_cff_mss_avg_pcp_nrm ${DATA}/dst/mie/aer_saharan_dust_01_086.nc
    ncks -H -u -C -F -v scv_cff_mss_avg_pcp_nrm ${DATA}/dst/mie/aer_saharan_dust_02_086.nc
    ncks -H -u -C -F -v scv_cff_mss_avg_pcp_nrm ${DATA}/dst/mie/aer_saharan_dust_03_086.nc
    ncks -H -u -C -F -v scv_cff_mss_avg_pcp_nrm ${DATA}/dst/mie/aer_saharan_dust_04_086.nc
#endif /* not 0 */
    ! fxm: hb increased first to size bins to improve transport
    real(r8),dimension(4),parameter::scv_cff_mss_nrm_cnv_dst_04bn=(/ 0.02,0.05,0.104947,0.267706 /) ! [m2 kg-1] Mass mean scavenging coefficient, precipitation-normalized, convective
    real(r8),dimension(4),parameter::scv_cff_mss_nrm_str_dst_04bn=(/ 0.03,0.1,0.197463,0.478069 /) ! [m2 kg-1] Mass mean scavenging coefficient, precipitation-normalized, stratiform
    !    real(r8),dimension(4),parameter::scv_cff_mss_nrm_cnv_dst_04bn=(/ 7.60833e-05,0.00408381,0.104947,0.267706 /) ! [m2 kg-1] Mass mean scavenging coefficient, precipitation-normalized, convective
    !    real(r8),dimension(4),parameter::scv_cff_mss_nrm_str_dst_04bn=(/ 0.000247209,0.00852661,0.197463,0.478069 /) ! [m2 kg-1] Mass mean scavenging coefficient, precipitation-normalized, stratiform
    ! Input
    ! Output
    ! Input/Output
    ! Local
    integer sz_idx            ! [idx] Counting index 
    ! Main code
    
    ! Scavenging ratios
    ! fxm: z_scv is only used in old wet deposition scheme, remove once PJR/MKB scheme fully adopted
    do sz_idx=1,dst_nbr
!       z_scv(sz_idx)=200.0_r8       ! [(kg dust/kg rain)/(kg dust/kg air)] Duce Atlantic DLM91, Prospero everywhere Pro96b. p. 37
       z_scv(sz_idx)=750.0_r8         ! [(kg dust/kg rain)/(kg dust/kg air)] Tegen everywhere TeF94 p. 22901
!       z_scv(1)=1000.0_r8           ! [(kg dust/kg rain)/(kg dust/kg air)] Uematsu (except N. Atl. Pro96b. p. 37
    end do                    ! end loop over cst
    
    ! Sanity check
    if(dmt_vwr(1) < 0.0001e-6_r8.or.dmt_vwr(1) > 100.0e-6_r8)  &
         stop 'ERROR: dst_scv_cmn_ini() probably called before dst_psd_ini()'
    ! Initialize common block used for wet deposition
    do sz_idx=1,dst_nbr
       ! frc_ice_scv = 1.0_r8 for nucleation scavenging exactly as efficient per unit mass in ice as in liquid
       ! frc_ice_scv = 0.0_r8 if nucleation scavenging does not occur in ice
       frc_ice_scv(sz_idx)=1.0_r8 ! [frc] Fraction of ice cloud allowing nucleation scavenging
    end do                     ! end loop over cst
    call ntp_vec(dst_nbr_hrz,dmt_ctr_hrz,scv_cff_pcp_nrm_cnv_dst_hrz, &
         dst_nbr,dmt_vwr,scv_cff_mss_avg_pcp_nrm_cnv, &
         xtr_typ_LHS,xtr_typ_RHS)
    call ntp_vec(dst_nbr_hrz,dmt_ctr_hrz,scv_cff_pcp_nrm_str_dst_hrz, &
         dst_nbr,dmt_vwr,scv_cff_mss_avg_pcp_nrm_str, &
         xtr_typ_LHS,xtr_typ_RHS)
    ! Override automatic grid with preset grid if available
    if (dst_nbr == 4) then
       do sz_idx=1,dst_nbr             
          scv_cff_mss_avg_pcp_nrm_cnv(sz_idx)=scv_cff_mss_nrm_cnv_dst_04bn(sz_idx) ! [m2 kg-1] Mass mean scavenging coefficient, precipitation-normalized, convective
          scv_cff_mss_avg_pcp_nrm_str(sz_idx)=scv_cff_mss_nrm_str_dst_04bn(sz_idx) ! [m2 kg-1] Mass mean scavenging coefficient, precipitation-normalized, stratiform
       end do                 ! end loop over cst
    endif                     ! endif dst_nbr == 4
    
    write (6,'(a)') 'Scavenging coefficient information:'
    write (6,'(a3,3(a10,1x))') 'idx','dmt_vwr','scv_cnv','scv_str'
    write (6,'(a3,3(a10,1x))') '','um','m2 kg-1','m2 kg-1'
    do sz_idx=1,dst_nbr
       write (6,'(i3,3(es10.3,1x))') sz_idx, &
            dmt_vwr(sz_idx)*1.0e6_r8,scv_cff_mss_avg_pcp_nrm_cnv(sz_idx),scv_cff_mss_avg_pcp_nrm_str(sz_idx)
    end do                     ! end loop over cst
    
    return
  end subroutine dst_scv_cmn_ini                       ! end dst_scv_cmn_ini()
  
  subroutine dst_scv(lchnk,ncol,obuf, &
       cld_frc,             & ! I [frc] Local total cloud fraction
       cld_frc_cnv,         & ! I/O [frc] Convective cloud fraction
       cld_vlm,             & ! I [frc] Cloud volume
       lat_idx,             & ! I [idx] Model latitude index
       prs_dlt,             & ! I [Pa] Pressure thickness
       prs_mdp,             & ! I [Pa] Pressure
       q_H2O_cnd,           & ! I [kg kg-1] Condensed H2O mixing ratio
       q_H2O_cnd2pcp_tnd,   & ! I/O [kg kg-1 s-1] Condensed H2O to precipitation tendency
       q_H2O_pcp2vpr_tnd,   & ! I/O [kg kg-1 s-1] H2O precipitation to vapor tendency
       q_H2O_vpr2pcp_cnv_tnd, & ! I [kg kg-1 s-1] H2O vapor to convective precipitation tendency
       q_dst,               & ! I/O [kg kg-1] Dust mixing ratio
       tm_adj,              & ! I [s] Adjustment timestep (CCM: 2*dt, MATCH: dt)
       tpt_mdp)             ! I [K] Temperature
    ! q_H2O_vpr2cnd_tnd,  & ! I [kg kg-1 s-1] H2O vapor to condensate tendency
    ! Purpose: Simulate wet deposition processes 
    ! dst_scv() is called by CCM:physics/aphys(), MATCH:src/physlic()
    ! Algorithm based on Barth, Rasch, and Kiehl (2000) (BRK00)
    use dstaer ! [mdl] Aerosol microphysical properties
    use dstbdg,only:bdg_aal,bdg_gam_wet ! [mdl] Mass budget diagnostics
    use dstblm,only:tpt_frz_pnt ! [mdl] Boundary layer meteorology for non-vegetated land surfaces
    use dstcst ! [mdl] Physical constants for dust routines
    use dstdbg ! [mdl] Debugging information for dust model
    use dstgrd,only:dst_nbr ! [mdl] Dust grid sizes
    use dstmssutl,only:dst_add_lon_lev,dst_add_lon ! [mdl] Mass budget utilities
    use dstnm ! [mdl] Nomenclature for outfld()
#ifndef BXM
    use histout,only:outfld ! [mdl] History/archiving
#endif /* BXM */
    use pmgrid ! [mdl] Spatial resolution parameters
    use precision ! [mdl] Precision r8, i8, ...
    use sng_mdl,only:ftn_strnul ! [mdl] String manipulation
    implicit none
    
    ! Parameters
    real(r8),parameter::pcp_flx_str_min=1.0e-12 ! [kg m-2 s-1] Minimum stratiform precipitation flux for determining evaporated fraction
    real(r8),parameter::q_H2O_cnd_min=1.0e-12 ! [kg kg-1] Minimum condensed water for determining condensation to precipitation conversion fraction
    real(r8),parameter::cld_frc_cll_min=1.0e-5 ! [frc] Minimum cloud fraction for collision scavenging for determining potential scavenging
    real(r8),parameter::q_dst_cld_min=1.0e-30 ! [kg kg-1] Epsilon to prevent divide by zero for interstitial fraction
    real(r8),parameter::q_trc_tnd_scv_eps=1.0e-30 ! [kg kg-1 s-1] Epsilon to prevent to prevent divide by zero in denominators of scavenged fractions
    
    ! Input
    integer,intent(in)::lat_idx ! I [idx] Model latitude index
    integer,intent(in)::lchnk ! I [id] Chunk identifier
    integer,intent(in)::ncol ! I [nbr] Number of atmospheric columns
    real(r8),intent(in)::obuf(*)  ! I [ptr] Output buffer
    real(r8),intent(in)::cld_frc(plond,plevp) ! I [frc] Local total cloud fraction
    real(r8),intent(in)::cld_vlm(plond,plev) ! I [frc] Cloud volume
    real(r8),intent(in)::q_H2O_cnd(plond,plev) ! I [kg kg-1] Condensed H2O mixing ratio
    real(r8),intent(in)::q_H2O_vpr2pcp_cnv_tnd(plond,plev) ! I [kg kg-1 s-1] H2O vapor to convective precipitation tendency
    real(r8),intent(in)::prs_dlt(plond,plev) ! I [Pa] Pressure thickness
    real(r8),intent(in)::prs_mdp(plond,plev) ! I [Pa] Midlayer pressure
    real(r8),intent(in)::tpt_mdp(plond,plev) ! I [K] Temperature
    real(r8),intent(in)::tm_adj   ! I [s] Adjustment timestep (CCM: 2*dt, MATCH: dt)
    ! Input/Output
    ! Fields besides q_dst are only altered when they are physically inconsistent
    real(r8),intent(inout)::cld_frc_cnv(plond,plev) ! I/O [frc] Convective cloud fraction
    real(r8),intent(inout)::q_H2O_pcp2vpr_tnd(plond,plev) ! I/O [kg kg-1 s-1] H2O precipitation to vapor tendency
    real(r8),intent(inout)::q_H2O_cnd2pcp_tnd(plond,plev) ! I/O [kg kg-1 s-1] Condensed H2O to precipitation tendency
    real(r8),intent(inout)::q_dst(plond,plev,dst_nbr) ! I/O [kg dust/kg moist air]
    ! Local Output
    real(r8) flx_mss_pcp_sfc(plond,dst_nbr) ! [kg m-2 s-1] Dust reaching surface in precipitation
    real(r8) flx_mss_pcp_sfc_ttl(plond) ! [kg m-2 s-1] Total dust reaching surface in precipitation
    real(r8) frc_trc_ist(plond,plev,dst_nbr) ! O [frc] Interstitial tracer fraction
    real(r8) pcp_flx_sfc(plond)   ! [kg m-2 s-1] Total precipitation reaching surface
    ! real(r8) prect2(plond)        ! [m s-1] Total precipitation reaching surface (diagnostic, should be same as PRECT)
    real(r8) q_dst_tnd_evp(plond,plev,dst_nbr) ! [kg kg-1 s-1] Evaporation tendency
    real(r8) q_dst_tnd_evp_ttl(plond,plev) ! [kg kg-1 s-1] Total evaporation tendency
    real(r8) q_dst_tnd_ncl(plond,plev,dst_nbr) ! O [kg kg-1 s-1] Nucleation scavenging tendency (positive definite)
    real(r8) q_dst_tnd_pcp(plond,plev,dst_nbr) ! [kg kg-1 s-1] Scavenging tendency
    real(r8) q_dst_tnd_pcp_ttl(plond,plev) ! [kg kg-1 s-1] Total scavenging tendency
    real(r8) q_dst_tnd_wet(plond,plev,dst_nbr) ! [kg kg-1 s-1] Wet deposition (evaporation minus scavenging) tendency
    real(r8) spc_xsx_ncl_scv(dst_nbr) ! [m2 kg-1] Specific cross section for nucleation scavenging, GBS98, BJG93
    real(r8) spc_xsx_cll_scv(dst_nbr) ! [m2 kg-1] Specific cross section for collision scavenging, GBS98, BJG93
    ! Local
    character(80) fl_out      ! [sng] Name of netCDF output file
    integer i                 ! [idx] lon index
    integer k                 ! [idx] lev index
    integer m                 ! [idx] cst index 
    real(r8) cld_frc_cll_cnv      ! [frc] Cloud fraction for collision scavenging by convective rain
    real(r8) cld_frc_cll_str      ! [frc] Cloud fraction for collision scavenging by stratiform rain
    real(r8) cld_frc_eff          ! [frc] Fraction of dust removable by precip
    real(r8) cld_frc_ncl_cnv      ! [frc] Cloud fraction for nucleation scavenging in convective clouds
    real(r8) cld_frc_ncl_str      ! [frc] Cloud fraction for nucleation scavenging in stratiform clouds
    ! real(r8) cnd_frc_ice          ! [frc] Ice fraction of condensate
    real(r8) dst2pcp_flx          ! [kg m-2 s-1] Local removal of dust by precip (q_H2O_wtr2pcp_ttl_tnd*q_dst*z_scv)
    real(r8) flx_mss_evp          ! [kg m-2 s-1] Local source of dust due to evaporation of precipitation from above (pcpfrc_evp*flx_mss_pcp_sfc)
    real(r8) flx_mss_scv_abv_cnv(plond,dst_nbr) ! [kg m-2 s-1] Tracer mass flux scavenged from above, convective
    real(r8) flx_mss_scv_abv_str(plond,dst_nbr) ! [kg m-2 s-1] Tracer mass flux scavenged from above, stratiform
    real(r8) mpl_air(plond,plev)  ! [kg m-2] Air mass path in layer
    real(r8) pcp2vpr_flx          ! [kg m-2 s-1] Local conversion of precip to vapor (q_H2O_pcp2vpr_tnd*mpl_air)
    real(r8) pcp_flx_cnv(plond)   ! [kg m-2 s-1] Convective precipitation flux
    real(r8) pcp_flx_str(plond)   ! [kg m-2 s-1] Stratiform precipitation flux
    real(r8) pcp_str_frc_evp      ! [frc] Stratiform precipitation from above that evaporates locally (pcp2vpr_flx/pcp_flx_sfc)
    real(r8) ptn_frc_scv_cll_cnv  ! [frc] Potential tracer fraction removed by collision scavenging in convective precipitation
    real(r8) ptn_frc_scv_cll_str  ! [frc] Potential tracer fraction removed by collisions scavenging in stratiform precipitation
    real(r8) ptn_frc_scv_ncl_cnv  ! [frc] Potential tracer fraction removed by nucleation scavenging in convective precipitation
    real(r8) ptn_frc_scv_ncl_str  ! [frc] Potential tracer fraction removed by nucleation scavenging in stratiform precipitation
    real(r8) q_H2O_cnd2pcp_frc    ! [frc] Fraction of condensate converted to (stratiform) precipitation
    real(r8) q_H2O_wtr2pcp_ttl_tnd(plond,plev) ! [kg kg-1 s-1] Local water to total precipitation tendency
    real(r8) q_trc_tnd_scv_cll_cnv ! [kg kg-1 s-1] Convective rain-collision scavenging tracer flux
    real(r8) q_trc_tnd_scv_cll_str ! [kg kg-1 s-1] Stratiform rain-collision scavenging tracer tendency
    real(r8) q_trc_tnd_scv_cnv    ! [kg kg-1 s-1] Total convective scavenging tracer tendency
    real(r8) q_trc_tnd_scv_ncl_cnv ! [kg kg-1 s-1] Convective nucleation scavenging tracer tendency
    real(r8) q_trc_tnd_scv_ncl_str ! [kg kg-1 s-1] Stratiform nucleation scavenging tracer tendency
    real(r8) q_trc_tnd_scv_str    ! [kg kg-1 s-1] Total stratitform scavenging tracer tendency
    real(r8) scv_ncl_frc_cnv      ! [frc] Nucleation scavenging fraction in convective clouds
    real(r8) scv_ncl_frc_str      ! [frc] Nucleation scavenging fraction in stratiform clouds
    real(r8) scv_tnd_fct          ! Factor limiting scavenging tendencies to amount present [frc]
    real(r8) tm_dlt               ! [s] Wet deposition timestep
    real(r8) tpt_cls              ! [C] Temperature
    real(r8) wtr2pcp_flx          ! [kg m-2 s-1] Local conversion of water to precip (q_H2O_wtr2pcp_ttl_tnd*mpl_air)
    ! real(r8) cld_frc_max_abv_cnv  ! [frc] Maximum convective cloud fraction at or above this level
    ! real(r8) cld_frc_max_abv_str  ! [frc] Maximum stratiform cloud fraction at or above this level
    ! real(r8) mpc_trc_abv          ! [kg m-2] Column integrated tracer path
    
    ! Main Code
    ! Timesplit if desired
    tm_dlt=tm_adj             ! [s] (default CCM: 2*dt, MATCH: dt)
    
    ! Initialize fluxes and tendencies
    q_dst_tnd_pcp=0.0_r8         ! [kg kg-1 s-1] Scavenging tendency
    q_dst_tnd_evp=0.0_r8         ! [kg kg-1 s-1] Evaporation tendency
    flx_mss_pcp_sfc=0.0_r8       ! [kg m-2 s-1]
    
    ! Specific cross-sections adopted were defined empirically by Balkanski
    ! fxm: specific cross-sections should use rain and aerosol size information
    ! Set size and species dependent scavenging cross-sections
    do m=1,dst_nbr
       ! spc_xsx_ncl_scv(m)=0.1_r8 ! [m2 kg-1] Specific cross section for nucleation scavenging, GBS98, BJG93
       spc_xsx_ncl_scv(m)=0.05_r8 ! [m2 kg-1] Specific cross section for nucleation scavenging, csz 50% of value used for hygrophilic aerosols
       ! spc_xsx_cll_scv(m)=0.1_r8 ! [m2 kg-1] Specific cross section for collision scavenging, GBS98, BJG93
       spc_xsx_cll_scv(m)=0.05_r8 ! [m2 kg-1] Specific cross section for collision scavenging, csz 50% of value used for hygrophilic aerosols
    end do                    ! end loop over cst
    
    ! Sanity checks
    ! Alter fields that are physically inconsistent
    ! fxm: Put this loop in an #ifdef DST_DBG block once the routines responsible are fixed
    do k=1,plev
       do i=1,plon
          ! q_H2O_pcp2vpr_tnd, and q_H2O_cnd2pcp_tnd may be < 0.0, though I do not understand why, so just make them positive definite
          q_H2O_pcp2vpr_tnd(i,k)=max(q_H2O_pcp2vpr_tnd(i,k),0.0_r8) ! [kg kg-1 s-1] 
          q_H2O_cnd2pcp_tnd(i,k)=max(q_H2O_cnd2pcp_tnd(i,k),0.0_r8) ! [kg kg-1 s-1] 
          ! if (q_H2O_pcp2vpr_tnd(i,k) < 0.0_r8) write(6,100) 'dst: dst_scv: lat = ',lat_idx,' q_H2O_pcp2vpr_tnd(',i,',',k,') = ',q_H2O_pcp2vpr_tnd(i,k)
          ! if (q_H2O_cnd2pcp_tnd(i,k) < 0.0_r8) write(6,100) 'dst: dst_scv: lat = ',lat_idx,' q_H2O_cnd2pcp_tnd(',i,',',k,') = ',q_H2O_cnd2pcp_tnd(i,k)
          ! 20000612: MATCH sets max(cld_frc=0.999) but allows cld_frc_cnv=1.0
          cld_frc_cnv(i,k)=min(cld_frc_cnv(i,k),cld_frc(i,k))
          ! if (cld_frc(i,k) < cld_frc_cnv(i,k)) then
          ! write(6,'(a)') 'dst: ERROR dst_scv() reports cloud fraction error:'
          ! write(6,'(a,i2,a,i3,a,i2,a,es15.9,a,es15.9)')
          ! $              'dst_pcp: lat = ',lat_idx,' cld_frc(',i,',',k,') = ',cld_frc(i,k)
          ! $              ,' < cld_frc_cnv = ',cld_frc_cnv(i,k)
          ! endif               ! endif
       end do                 ! end loop over lon
    end do                    ! end loop over lev
    ! 100 format(a,i2,a,i3,a,i2,a,es8.1) 
    
    ! Compute necessary derived fields
    do k=1,plev
       do i=1,plon
          ! Mass of air currently in gridbox
          mpl_air(i,k)=prs_dlt(i,k)*grv_sfc_rcp ! [kg m-2]
          ! Total precipitation formation
          q_H2O_wtr2pcp_ttl_tnd(i,k)= & ! [kg kg-1 s-1] H2O vapor + condensate conversion to precipitation tendency
               q_H2O_cnd2pcp_tnd(i,k)+q_H2O_vpr2pcp_cnv_tnd(i,k)
       end do                 ! end loop over lon
    end do                    ! end loop over lev
    
    ! Keep track of amount of stratiform precip and tracer falling from above
    pcp_flx_str=0.0_r8           ! [kg m-2 s-1] Stratiform precipitation flux
    pcp_flx_cnv=0.0_r8           ! [kg m-2 s-1] Convective precipitation flux
    flx_mss_scv_abv_str=0.0_r8   ! [kg m-2 s-1] Tracer mass flux scavenged from above, stratiform
    flx_mss_scv_abv_cnv=0.0_r8   ! [kg m-2 s-1] Tracer mass flux scavenged from above, convective
    
    ! Notes on algorithm:
    ! This algorithm originated by PJR and MKB for highly soluble aerosols (sulfate)
    ! For some species, e.g., HNO3, any tracer within cloud volume is in cloud water
    ! For other species, e.g., mineral dust, amount in cloud water varies
    ! Convective clouds: We do not know cloud water amount 
    ! Assume all convective cloud water (and thus all tracer in convective cloud water) falls out each time step
    ! Stratiform clouds: Fraction of cloud water converted to precipitation defines amount of tracer which is scavenged
    
    ! Invert loops (lon is inner dimension) process from top down on each longitude
    ! Surface fluxes accumulate as local scavenging and evaporation modify falling precipitation
    ! Top-down structure of algorithm based on CCM2 cond() routine of J. J. Hack
    do k=1,plev               ! NB: Inefficient loop order
       do i=1,plon
          
          tpt_cls=tpt_mdp(i,k)-tpt_frz_pnt ! [C] Temperature
          
          ! Ice fraction is currently not used in dust wet deposition
          ! PJR and MCB use cnd_frc_ice to disallow snow from contributing to nucleation scavenging (of, e.g., SO4)
          ! CSZ allows dust to nucleate ice, thus allows snow to remove dust
          ! cnd_frc_ice=max(0.0_r8,min(-tpt_cls*0.05,1.0_r8)) ! [frc] Ice fraction of condensate
          
          ! Convert local condensation and evaporation tendencies to fluxes
          wtr2pcp_flx=q_H2O_wtr2pcp_ttl_tnd(i,k)*mpl_air(i,k) ! [kg m-2 s-1] Local conversion of water to precip
          pcp2vpr_flx=q_H2O_pcp2vpr_tnd(i,k)*mpl_air(i,k) ! [kg m-2 s-1] Local conversion of precip to vapor
          
          ! Compute fraction of stratiform rain from above that evaporates in this gridcell
          ! This fraction will be applied to tracer in stratitform rain from above to compute
          ! tracer released into this gridcell during evaporation of stratiform rain.
          ! Use safety limits because pcp_str_frc_evp often equals, e.g., 1.0000000000000007
          pcp_str_frc_evp=min(pcp2vpr_flx/max(pcp_flx_str_min,pcp_flx_str(i)),1.0_r8) ! [frc]
          
          ! Compute fraction of condensate converted to (stratiform) precipitation
          ! This fraction determines what fraction of tracer in stratiform clouds is potentially nucleation scavenged
          ! [frc] Fraction of condensate converted to (stratiform) precipitation
          q_H2O_cnd2pcp_frc=max(0.0_r8,min(1.0_r8, &
               q_H2O_cnd2pcp_tnd(i,k)*tm_dlt/max(q_H2O_cnd(i,k),q_H2O_cnd_min)))
          ! fxm: PJR and MCB decided not to add condensate formed in current timestep to denominator, why?
          ! $              q_H2O_cnd2pcp_tnd(i,k)*tm_dlt/max(q_H2O_cnd(i,k)+q_H2O_vpr2cnd_tnd(i,k)*tm_dlt,q_H2O_cnd_min)))
          
          ! Cloud fraction properties for each process are independent of aerosol:
          ! MCB and PJR considered multiple candidates for each process, but chose
          ! to use cld_vlm for all SO4 processes
          
          ! Cloud used in convective nucleation scavenging should be local gridcell amount
          ! MCB and PJR considered cld_frc, cld_frc_cnv, and cld_vlm for SO4
          cld_frc_ncl_cnv=cld_frc_cnv(i,k) ! [frc] Cloud fraction for nucleation scavenging in convective clouds
          ! Cloud in convective rain-collision scavenging depends on local and higher cloud
          ! MCB and PJR considered max(cld_frc,cld_frc_abv_cnv), max(cld_frc_cnv,cld_frc_abv_cnv), max(cld_vlm,cld_frc_abv_cnv), and simply cld_vlm for SO4
          cld_frc_cll_cnv=cld_vlm(i,k) ! [frc] Cloud fraction for collision scavenging by convective rain
          ! Cloud used in stratiform nucleation scavenging should be local gridcell amount
          ! MCB and PJR considered cld_frc-cld_frc_cnv, cld_frc, and cld_vlm for SO4
          cld_frc_ncl_str=cld_frc(i,k)-cld_frc_cnv(i,k) ! [frc] Cloud fraction for nucleation scavenging in stratiform clouds
          ! Cloud in stratiform rain-collision scavenging depends on local and higher cloud
          ! MCB and PJR considered max(cld_frc,cld_frc_abv_str), max(cld_vlm,cld_frc_abv_str), and simply cld_vlm for SO4
          cld_frc_cll_str=cld_vlm(i,k) ! [frc] Cloud fraction for collision scavenging by stratiform rain
          
          ! Scavenging processes may be species and size dependent
          do m=1,dst_nbr
             
             ! Scavenging in convective clouds:
             ! Nucleation scavenging (SeP97 p. 1027):
             ! In-cloud scavenging occurs through nucleation scavenging and interstitial aerosol collection by cloud droplets (as opposed to raindrops)
             ! Nucleation scavenging is the growth (activation) of CCN to cloud drops
             ! The following representation of nucleation scavenging involves a number of assumptions:
             ! 1. Nucleation scavenging permanently removes aerosol from cloud, in-cloud re-evaporation (de-activation) is not allowed
             ! 2. Nucleation scavenging in convective clouds is treated here, but the interstitial fraction is not. fxm: this would be somewhat bizarre if it were true
             ! 
             
             ! Interstitial aerosol collection by cloud droplets is inefficient unless the aerosol or the droplet is larger than about 20 microns in diameter
             ! However, removal of interstitial aerosol is likely to be much more efficient in convective updrafts than in the stratiform region
             ! PJR's wet deposition and convection scheme apparently handle interstitial coalescence separately, but based on the interstitial fraction computed here
             ! fxm: need to understand whether some of interstitial fraction is removed in convection routines
             
             ! For dust, interstitial collection by droplets is rare because large dust particles settle out quickly, before precipitation has a chance to act
             
             ! Convective precipitation formation during timestep times specific cross-section
             ! for nucleation scavenging yields fraction of homogeneously distributed tracer
             ! in liquid cloud that is potentially nucleation scavenged this timestep
             ! Species-dependence is incorporated through spc_xsx_ncl_scv factor
             ! [frc] Potential tracer fraction removed by nucleation scavenging in convective precipitation
             ptn_frc_scv_ncl_cnv=max(0.0_r8,min(1.0_r8, &
                  q_H2O_vpr2pcp_cnv_tnd(i,k)*mpl_air(i,k)*spc_xsx_ncl_scv(m)*tm_dlt))
             
             ! [kg kg-1 s-1] Convective nucleation scavenging tracer tendency
             ! Allow dust to nucleate ice, thus allow snow to remove dust
             q_trc_tnd_scv_ncl_cnv=cld_frc_ncl_cnv*ptn_frc_scv_ncl_cnv*q_dst(i,k,m)/tm_dlt ! csz
             ! Following line disallows snow from contributing to nucleation scavenging
             ! q_trc_tnd_scv_ncl_cnv=cld_frc_ncl_cnv*ptn_frc_scv_ncl_cnv*q_dst(i,k,m)*(1.0-cnd_frc_ice)/tm_dlt
             
             ! Convective rain-collision scavenging (SeP97 p. 1021):
             ! Proportional to cross-sectional area of gridcell swept by precipitation 
             ! Collision efficiency E is probability that collision occurs within the
             ! cross-sectional area swept out by the path of the falling raindrop.
             ! For rain and hygroscopic aerosol the collision efficiency is the same
             ! as the collection efficiency because the sticking efficiency is ~ 1.0. 
             ! E is usually << 1 for atmospheric aerosol due to streamlines diverging around  around the falling raindrop
             ! For very small (D < 0.1 um) particles, Brownian diffusion enhances collection
             ! For somewhat large (D > 1.0 um), inertial impaction increases collection
             ! In between (0.1 < D < 1.0 um) is the "Greenfield gap" where E << 1
             ! A "reasonable" raindrop radius is ~ 1 mm RoY94 p. 179
             
             ! Area flux = pcp_flx [kg m-2 s-1] * pi rds^2       [m2 H2O] * dt [s]
             ! ------- ------------   -------------- --------  
             ! dns_H2O [kg m-3 H2O]   (4/3) pi rds^3 [m3 H2O]
             
             ! = 3*rds*pcp_flx*dt [m2 H2O m-2]
             ! ----------------
             ! 4
             
             ! [frc] Potential tracer fraction removed by collision scavenging in convective precipitation
             ptn_frc_scv_cll_cnv=max(0.0_r8,min(1.0_r8, &
                  pcp_flx_cnv(i)*spc_xsx_cll_scv(m)*tm_dlt/max(cld_frc_cll_cnv,cld_frc_cll_min)))
             
             ! [kg kg-1 s-1] Convective rain-collision scavenging tracer tendency
             q_trc_tnd_scv_cll_cnv=cld_frc_cll_cnv*ptn_frc_scv_cll_cnv*q_dst(i,k,m)/tm_dlt
             
             ! [kg kg-1 s-1] Total convective scavenging tracer tendency
             q_trc_tnd_scv_cnv=q_trc_tnd_scv_ncl_cnv+q_trc_tnd_scv_cll_cnv
             
             ! [frc] Nucleation scavenging fraction in convective clouds
             scv_ncl_frc_cnv=q_trc_tnd_scv_ncl_cnv/(q_trc_tnd_scv_cnv+q_trc_tnd_scv_eps) 
             
             ! Scavenging in stratiform clouds:
             
             ! Nucleation scavenging (SeP97 p. 1027):
             ! This is handled slightly differently than in the convective case
             ! First we determine fraction of local condensate that precipitates
             ! We assume same fraction of tracer is removed from liquid portion of cloud
             ! Species-dependence is currently not incorporated, i.e., there is no
             ! factor to account for the scavenging efficiency for in-cloud nucleation
             
             ! [kg kg-1 s-1] Stratiform nucleation scavenging tracer tendency
             ! Allow dust to nucleate ice, thus allow snow to remove dust
             q_trc_tnd_scv_ncl_str=cld_frc_ncl_str*q_H2O_cnd2pcp_frc*q_dst(i,k,m)/tm_dlt ! csz
             ! Following line disallows snow from contributing to nucleation scavenging
             ! q_trc_tnd_scv_ncl_str=cld_frc_ncl_str*q_H2O_cnd2pcp_frc*q_dst(i,k,m)*(1.0-cnd_frc_ice)/tm_dlt
             
             ! Stratiform rain-collision scavenging (SeP97 p. 1021):
             
             ! [frc] Potential tracer fraction removed by collisions scavenging in stratiform precipitation
             ptn_frc_scv_cll_str=max(0.0_r8,min(1.0_r8, &
                  pcp_flx_str(i)*spc_xsx_cll_scv(m)*tm_dlt/max(cld_frc_cll_str,cld_frc_cll_min)))
             
             ! [kg kg-1 s-1] Stratiform rain-collision scavenging tracer tendency
             q_trc_tnd_scv_cll_str=cld_frc_cll_str*ptn_frc_scv_cll_str*q_dst(i,k,m)/tm_dlt
             
             ! [kg kg-1 s-1] Total stratiform scavenging tracer tendency
             q_trc_tnd_scv_str=q_trc_tnd_scv_ncl_str+q_trc_tnd_scv_cll_str
             
             ! [frc] Nucleation scavenging fraction in stratiform clouds
             scv_ncl_frc_str=q_trc_tnd_scv_ncl_str/(q_trc_tnd_scv_str+q_trc_tnd_scv_eps) 
             
             ! Ensure sum of convective and stratiform scavenging tendencies does not exceed amount present
             scv_tnd_fct=q_dst(i,k,m)/max(tm_dlt*(q_trc_tnd_scv_cnv+q_trc_tnd_scv_str),q_trc_tnd_scv_eps) ! [frc]
             if (scv_tnd_fct < 1.0_r8) then
                ! Total stratiform scavenging tracer tendency
                q_trc_tnd_scv_str=q_trc_tnd_scv_str*scv_tnd_fct ! [kg kg-1 s-1]
                ! Total convective scavenging tracer tendency
                q_trc_tnd_scv_cnv=q_trc_tnd_scv_cnv*scv_tnd_fct ! [kg kg-1 s-1]
                ! It is now "safe" to use these tendencies to adjust the tracer mixing ratio
                ! However, it is only "safe" to machine precision since order of operations can still cause problems
             endif            ! endif
             
             ! Nomenclature: 
             ! Scavenging tendency refers to removal by nucleation and collision scavenging [positive definite]
             ! Evaporation tendency refers to source by evaporation of stratiform precipitation from above [positive definite]
             ! Wet deposition tendency is evaporation tendency (source) minus scavenging tendency (sink) [positive when source > sink]
             ! [kg kg-1 s-1] Scavenging tendency
             q_dst_tnd_pcp(i,k,m)=q_trc_tnd_scv_str+q_trc_tnd_scv_cnv 
             
             ! [kg kg-1 s-1] Evaporation tendency
             q_dst_tnd_evp(i,k,m)=pcp_str_frc_evp*flx_mss_scv_abv_str(i,m)/mpl_air(i,k)
             
             ! Tracer not removed within cloud is interstitial and subject to convective transport
             frc_trc_ist(i,k,m)= & ! [frc] Interstitial tracer fraction
                  1.0_r8-max(0.0_r8,min(1.0_r8, &
                  q_dst_tnd_pcp(i,k,m)*tm_dlt/max(cld_vlm(i,k)*q_dst(i,k,m),q_dst_cld_min)))
             
             ! Diagnose nucleation scavenging tendency for possible archival (superfluous, could be deleted)
             q_dst_tnd_ncl(i,k,m)= & ! [kg kg-1 s-1] Nucleation scavenging tendency
                  q_trc_tnd_scv_cnv*scv_ncl_frc_cnv+q_trc_tnd_scv_str*scv_ncl_frc_str
             
             ! Diagnose wet deposition (evaporation minus scavenging) tendency for possible archival (superflous, could be deleted)
             q_dst_tnd_wet(i,k,m)=q_dst_tnd_evp(i,k,m)-q_dst_tnd_pcp(i,k,m) ! [kg kg-1 s-1]
             
             ! Tendencies are known, so adjust mixing ratios
             ! max() clause is necessary because result cannot be guarranteed positive otherwise
             q_dst(i,k,m)=max(0.0_r8,q_dst(i,k,m)+tm_dlt*q_dst_tnd_wet(i,k,m)) ! [kg kg-1] Dust mixing ratio
             
             ! Accumulate aerosol-dependent vertical integrals
             ! All tracer in stratiform rain may potentially be evaporated at every level
             flx_mss_scv_abv_str(i,m)=flx_mss_scv_abv_str(i,m)*(1.0_r8-pcp_str_frc_evp)+q_trc_tnd_scv_str*mpl_air(i,k) ! [kg m-2 s-1] Tracer mass flux scavenged from above, stratiform
             ! Tracer in convective rain increases monotonically on way down--convective evaporation is not accounted for
             flx_mss_scv_abv_cnv(i,m)=flx_mss_scv_abv_cnv(i,m)+q_trc_tnd_scv_cnv*mpl_air(i,k) ! [kg m-2 s-1] Tracer mass flux scavenged from above, convective
             
          end do              ! end loop over cst
          
          ! Accumulate aerosol-independent vertical integrals
          ! Precipitation falling into cell below is precipitation that entered this cell from above, 
          ! plus local source of precipitation, minus what was evaporated. 
          pcp_flx_str(i)=pcp_flx_str(i)+(q_H2O_cnd2pcp_tnd(i,k)-q_H2O_pcp2vpr_tnd(i,k))*mpl_air(i,k) ! [kg m-2 s-1] Stratiform precipitation flux
          ! Evaporation of convective precipitation is not allowed here
          pcp_flx_cnv(i)=pcp_flx_cnv(i)+q_H2O_vpr2pcp_cnv_tnd(i,k)*mpl_air(i,k) ! [kg m-2 s-1] Convective precipitation flux
          
       end do                 ! end loop over lon
    end do                    ! end loop over lev
    
#ifdef DST_DBG
    ! Sanity check
    do m=1,dst_nbr
       do k=1,plev
          do i=1,plon
             if (q_dst(i,k,m) < 0.0_r8 .or. q_dst(i,k,m) > q_dst_mxm) then
                write(6,'(a)') 'dst: ERROR dst_scv() reports mixing ratio too high or too low:'
                write(6,'(a,i2,a,i3,a,i2,a,i2,a,es8.1,a,2(a,es8.1,a))') &
                     'dst_pcp: lat = ',lat_idx,' q_dst(',i,',',k,',',m,') = ',q_dst(i,k,m),' kg kg-1' &
                     ,', q_dst_tnd_evp = ',q_dst_tnd_evp(i,k,m),' kg kg-1 s-1' &
                     ,', q_dst_tnd_pcp = ',q_dst_tnd_pcp(i,k,m),' kg kg-1 s-1'
                stop
             endif            ! endif err
          end do              ! end loop over lon
       end do                 ! end loop over lev
    end do                    ! end loop over cst
#endif /* not DST_DBG */
    
    ! Diagnostic totals
    do i=1,plon
       pcp_flx_sfc(i)=pcp_flx_str(i)+pcp_flx_cnv(i) ! [kg m-2 s-1] Total precipitation reaching surface
       ! c     Convert [kg m-2 s-1] to [m s-1]
       ! prect2(i)=pcp_flx_sfc(i)*0.001 ! [m s-1] Total precipitation reaching surface (same as PRECT)
    end do                    ! end loop over lon
    do m=1,dst_nbr
       do i=1,plon
          ! Tracer reaching surface in precipitation
          flx_mss_pcp_sfc(i,m)=flx_mss_scv_abv_str(i,m)+flx_mss_scv_abv_cnv(i,m) ! [kg m-2 s-1]
       end do                 ! end loop over cst
    end do                    ! end loop over lon
    
    ! Integrate over all size categories and levels for diagnostic output
    call dst_add_lon_lev(q_dst_tnd_evp,q_dst_tnd_evp_ttl)
    call dst_add_lon_lev(q_dst_tnd_pcp,q_dst_tnd_pcp_ttl)
    call dst_add_lon(flx_mss_pcp_sfc,flx_mss_pcp_sfc_ttl)
    
#ifdef BXM
    ! Update netCDF file
    fl_out='aer.nc'           ! [sng] Name of netCDF output file
    call ftn_strnul(fl_out)
    call dpsscv2nc(             &
         cld_frc,             & ! I [frc] Local total cloud fraction
         cld_frc_cnv,         & ! I [frc] Convective cloud fraction
         cld_vlm,             & ! I [frc] Cloud volume
         fl_out,              & ! I [sng] Name of netCDF output file
         flx_mss_pcp_sfc,     & ! I [kg m-2 s-1] Dust reaching surface in precipitation
         flx_mss_pcp_sfc_ttl, & ! I [kg m-2 s-1] Total dust reaching surface in precipitation
         frc_trc_ist,         & ! I [frc] Interstitial tracer fraction
         lat_idx,             & ! I [idx] Model latitude index
         pcp_flx_sfc,         & ! I [kg m-2 s-1] Total precipitation reaching surface
         q_H2O_cnd,           & ! I [kg kg-1] Condensed H2O mixing ratio
         q_H2O_cnd2pcp_tnd,   & ! I [kg kg-1 s-1] Condensed H2O to precipitation tendency
         q_H2O_pcp2vpr_tnd,   & ! I [kg kg-1 s-1] H2O precipitation to vapor tendency
         q_H2O_vpr2pcp_cnv_tnd, & ! I [kg kg-1 s-1] H2O vapor to convective precipitation tendency
         q_dst_tnd_evp,       & ! I [kg kg-1 s-1] Evaporation tendency
         q_dst_tnd_evp_ttl,   & ! I [kg kg-1 s-1] Total evaporation tendency
         q_dst_tnd_ncl,       & ! I [kg kg-1 s-1] Nucleation scavenging tendency
         q_dst_tnd_pcp,       & ! I [kg kg-1 s-1] Scavenging tendency
         q_dst_tnd_pcp_ttl,   & ! I [kg kg-1 s-1] Total scavenging tendency
         q_dst_tnd_wet,       & ! I [kg kg-1 s-1] Wet deposition (evaporation minus scavenging) tendency
         spc_xsx_cll_scv,     & ! I [m2 kg-1] Specific cross section for collision scavenging
         spc_xsx_ncl_scv)     ! I [m2 kg-1] Specific cross section for nucleation scavenging
    
#endif /* not BXM */
#ifdef CCM
    call outfld('DSTSFPCP',flx_mss_pcp_sfc_ttl,ncol,lchnk)
    call outfld('DSTSSPCP',q_dst_tnd_pcp_ttl,ncol,lchnk)
    call outfld('DSTSSEVP',q_dst_tnd_evp_ttl,ncol,lchnk)
    do m=1,dst_nbr
       call outfld(flx_mss_pcp_sfc_nm(m),flx_mss_pcp_sfc(1,m),ncol,lchnk)
    end do                    ! end loop over cst
#else /* not CCM */
    ! call outfld('PRECT2',prect2,plond,lat_idx,obuf)
    call outfld('DSTSFPCP',flx_mss_pcp_sfc_ttl,plond,lat_idx,obuf)
    call outfld('DSTSSPCP',q_dst_tnd_pcp_ttl,plond,lat_idx,obuf)
    call outfld('DSTSSEVP',q_dst_tnd_evp_ttl,plond,lat_idx,obuf)
    do m=1,dst_nbr
       call outfld(flx_mss_pcp_sfc_nm(m),flx_mss_pcp_sfc(1,m),plond,lat_idx,obuf)
    end do                    ! end loop over cst
#endif /* not CCM */
#ifdef DST_MSS_BDG
    call bdg_aal('dst_sf_pcp',lat_idx,flx_mss_pcp_sfc_ttl)
    call bdg_gam_wet('dst_ss_pcp',lat_idx,prs_dlt,q_dst_tnd_pcp_ttl)
    call bdg_gam_wet('dst_ss_evp',lat_idx,prs_dlt,q_dst_tnd_evp_ttl)
#endif /* not DST_MSS_BDG */
    return
  end subroutine dst_scv                       ! end dst_scv()
  
  subroutine dpsscv2nc(             &
       cld_frc,             & ! I [frc] Local total cloud fraction
       cld_frc_cnv,         & ! I [frc] Convective cloud fraction
       cld_vlm,             & ! I [frc] Cloud volume
       fl_out,              & ! I [sng] Name of netCDF output file
       flx_mss_pcp_sfc,     & ! I [kg m-2 s-1] Dust reaching surface in precipitation
       flx_mss_pcp_sfc_ttl, & ! I [kg m-2 s-1] Total dust reaching surface in precipitation
       frc_trc_ist,         & ! I [frc] Interstitial tracer fraction
       lat_idx,             & ! I [idx] Model latitude index
       pcp_flx_sfc,         & ! I [kg m-2 s-1] Total precipitation reaching surface
       q_H2O_cnd,           & ! I [kg kg-1] Condensed H2O mixing ratio
       q_H2O_cnd2pcp_tnd,   & ! I [kg kg-1 s-1] Condensed H2O to precipitation tendency
       q_H2O_pcp2vpr_tnd,   & ! I [kg kg-1 s-1] H2O precipitation to vapor tendency
       q_H2O_vpr2pcp_cnv_tnd, & ! I [kg kg-1 s-1] H2O vapor to convective precipitation tendency
       q_dst_tnd_evp,       & ! I [kg kg-1 s-1] Evaporation tendency
       q_dst_tnd_evp_ttl,   & ! I [kg kg-1 s-1] Total evaporation tendency
       q_dst_tnd_ncl,       & ! I [kg kg-1 s-1] Nucleation scavenging tendency
       q_dst_tnd_pcp,       & ! I [kg kg-1 s-1] Scavenging tendency
       q_dst_tnd_pcp_ttl,   & ! I [kg kg-1 s-1] Total scavenging tendency
       q_dst_tnd_wet,       & ! I [kg kg-1 s-1] Wet deposition (evaporation minus scavenging) tendency
       spc_xsx_cll_scv,     & ! I [m2 kg-1] Specific cross section for collision scavenging
       spc_xsx_ncl_scv)     ! I [m2 kg-1] Specific cross section for nucleation scavenging
    ! Purpose: Output time-varying wet deposition properties to netCDF file
    use netcdf ! [mdl] netCDF interface
    use nf90_utl ! [mdl] netCDF utilities
    use precision ! [mdl] Precision r8, i8, ...
    use dbg_mdl ! [mdl] Debugging constants, prg_nm, dbg_lvl
    use dstgrd ! [mdl] Dust grid sizes
    use pmgrid ! [mdl] Spatial resolution parameters
    use dstctl ! [mdl] Control variables, routines
    use sng_mdl,only:ftn_strlen ! [mdl] String manipulation
    implicit none
    ! Parameters
    character(len=*),parameter::sbr_nm='dpsscv2nc' ! [sng] Subroutine name
    ! Input
    character(len=*),intent(in)::fl_out ! [sng] Name of netCDF output file
    integer,intent(in)::lat_idx ! I [idx] Model latitude index
    real(r8),intent(in)::cld_frc(plond,plev) ! [frc] Cloud fraction
    real(r8),intent(in)::cld_frc_cnv(plond,plev) ! [frc] Convective cloud fraction
    real(r8),intent(in)::cld_vlm(plond,plev) ! [frc] Cloud volume
    real(r8),intent(in)::q_H2O_vpr2pcp_cnv_tnd(plond,plev) ! [kg kg-1 s-1] H2O vapor to convective precipitation tendency
    real(r8),intent(in)::q_H2O_pcp2vpr_tnd(plond,plev) ! [kg kg-1 s-1] H2O precipitation to vapor tendency
    real(r8),intent(in)::q_H2O_cnd(plond,plev) ! [kg kg-1] Condensed H2O mixing ratio
    real(r8),intent(in)::q_H2O_cnd2pcp_tnd(plond,plev) ! [kg kg-1 s-1] Condensed H2O to precipitation tendency
    real(r8),intent(in)::spc_xsx_ncl_scv(dst_nbr) ! [m2 kg-1] Specific cross section for nucleation scavenging
    real(r8),intent(in)::spc_xsx_cll_scv(dst_nbr) ! [m2 kg-1] Specific cross section for collision scavenging
    real(r8),intent(in)::flx_mss_pcp_sfc(plond,dst_nbr) ! [kg m-2 s-1] Dust reaching surface in precipitation
    real(r8),intent(in)::flx_mss_pcp_sfc_ttl(plond) ! [kg m-2 s-1] Total dust reaching surface in precipitation
    real(r8),intent(in)::frc_trc_ist(plond,plev,dst_nbr) ! O [frc] Interstitial tracer fraction
    real(r8),intent(in)::pcp_flx_sfc(plond) ! [kg m-2 s-1] Total precipitation reaching surface
    real(r8),intent(in)::q_dst_tnd_evp(plond,plev,dst_nbr) ! [kg kg-1 s-1] Evaporation tendency
    real(r8),intent(in)::q_dst_tnd_evp_ttl(plond,plev) ! [kg kg-1 s-1] Total evaporation tendency
    real(r8),intent(in)::q_dst_tnd_ncl(plond,plev,dst_nbr) ! O [kg kg-1 s-1] Nucleation scavenging tendency
    real(r8),intent(in)::q_dst_tnd_pcp(plond,plev,dst_nbr) ! [kg kg-1 s-1] Scavenging tendency
    real(r8),intent(in)::q_dst_tnd_pcp_ttl(plond,plev) ! [kg kg-1 s-1] Total scavenging tendency
    real(r8),intent(in)::q_dst_tnd_wet(plond,plev,dst_nbr) ! [kg kg-1 s-1] Wet deposition (evaporation minus scavenging) tendency
    ! Output
    ! Local
    ! File metadata and dimension IDs
    integer cnt_lon_lev_sz_time(4) ! Count array
    integer cnt_lon_lev_time(3) ! Count array
    integer cnt_lon_sz_time(3) ! Count array
    integer cnt_lon_time(2)   ! Count array
    integer dim_lon_lev_sz_time(4) ! [enm] Dimension IDs
    integer dim_lon_lev_time(3) ! [enm] Dimension IDs
    integer dim_lon_sz_time(3) ! [enm] Dimension IDs
    integer dim_lon_time(2)   ! [enm] Dimension IDs
    integer fll_mode_old      ! Old fill mode
    integer lev_dim_id        ! [enm] Dimension ID for lev
    integer lon_dim_id        ! [enm] Dimension ID for lon
    integer nc_id             ! File handle
    integer rcd               ! [rcd] Return success code
    integer srt_lon_lev_sz_time(4) ! Starting index array
    integer srt_lon_lev_time(3) ! Starting index array
    integer srt_lon_sz_time(3) ! Starting index array
    integer srt_lon_time(2)   ! Starting index array
    integer sz_dim_id         ! [enm] Dimension ID for sz
    integer time_dim_id       ! [enm] Dimension ID for time
    ! Variable IDs
    integer cld_frc_cnv_id    ! [enm] Variable ID
    integer cld_frc_id        ! [enm] Variable ID
    integer cld_vlm_id        ! [enm] Variable ID
    integer q_H2O_cnd_id      ! [enm] Variable ID
    integer q_H2O_cnd2pcp_tnd_id ! [enm] Variable ID
    integer q_H2O_pcp2vpr_tnd_id ! [enm] Variable ID
    integer q_H2O_vpr2pcp_cnv_tnd_id ! [enm] Variable ID
    integer spc_xsx_ncl_scv_id ! [enm] Variable ID
    integer spc_xsx_cll_scv_id ! [enm] Variable ID
    integer flx_mss_pcp_sfc_id ! [enm] Variable ID
    integer flx_mss_pcp_sfc_ttl_id ! [enm] Variable ID
    integer frc_trc_ist_id    ! [enm] Variable ID
    integer pcp_flx_sfc_id    ! [enm] Variable ID
    integer q_dst_tnd_evp_id  ! [enm] Variable ID
    integer q_dst_tnd_evp_ttl_id ! [enm] Variable ID
    integer q_dst_tnd_ncl_id  ! [enm] Variable ID
    integer q_dst_tnd_pcp_id  ! [enm] Variable ID
    integer q_dst_tnd_pcp_ttl_id ! [enm] Variable ID
    integer q_dst_tnd_wet_id  ! [enm] Variable ID
    ! Variable data
    
    ! Begin netCDF output routines
    rcd=nf90_noerr              ! nf90_noerr == 0
    rcd=nf90_wrp_open(fl_out,nf90_write,nc_id,sbr_nm=sbr_nm)
    rcd=rcd+nf90_redef(nc_id)
    rcd=nf90_wrp_inq_dimid(nc_id,'lev',lev_dim_id)
    rcd=nf90_wrp_inq_dimid(nc_id,'lon',lon_dim_id)
    rcd=nf90_wrp_inq_dimid(nc_id,'time',time_dim_id)
    rcd=nf90_wrp_inq_dimid(nc_id,'sz',sz_dim_id)
    ! Add global attributes
    ! Define dimension IDs
    ! Assemble ID and count vectors for each multidimensional combination of dimensions
    dim_lon_time=(/lon_dim_id,time_dim_id/)
    cnt_lon_time=(/plon,1/)
    srt_lon_time=(/1,nstep/)
    
    dim_lon_sz_time=(/lon_dim_id,sz_dim_id,time_dim_id/)
    cnt_lon_sz_time=(/plon,dst_nbr,1/)
    srt_lon_sz_time=(/1,1,nstep/)
    
    dim_lon_lev_time=(/lon_dim_id,lev_dim_id,time_dim_id/)
    cnt_lon_lev_time=(/plon,plev,1/)
    srt_lon_lev_time=(/1,1,nstep/)
    
    dim_lon_lev_sz_time=(/lon_dim_id,lev_dim_id,sz_dim_id,time_dim_id/)
    cnt_lon_lev_sz_time=(/plon,plev,dst_nbr,1/)
    srt_lon_lev_sz_time=(/1,1,1,nstep/)
    
    if (nstep == 1) then
       ! Variable definitions
       rcd=rcd+nf90_def_var(nc_id,'cld_frc',nf90_float,dim_lon_lev_time,cld_frc_id)
       rcd=rcd+nf90_def_var(nc_id,'cld_frc_cnv',nf90_float,dim_lon_lev_time,cld_frc_cnv_id)
       rcd=rcd+nf90_def_var(nc_id,'cld_vlm',nf90_float,dim_lon_lev_time,cld_vlm_id)
       rcd=rcd+nf90_def_var(nc_id,'flx_mss_pcp_sfc',nf90_float,dim_lon_sz_time,flx_mss_pcp_sfc_id)
       rcd=rcd+nf90_def_var(nc_id,'flx_mss_pcp_sfc_ttl',nf90_float,dim_lon_time,flx_mss_pcp_sfc_ttl_id)
       rcd=rcd+nf90_def_var(nc_id,'frc_trc_ist',nf90_float,dim_lon_lev_sz_time,frc_trc_ist_id)
       rcd=rcd+nf90_def_var(nc_id,'pcp_flx_sfc',nf90_float,dim_lon_time,pcp_flx_sfc_id)
       rcd=rcd+nf90_def_var(nc_id,'q_H2O_cnd',nf90_float,dim_lon_lev_time,q_H2O_cnd_id)
       rcd=rcd+nf90_def_var(nc_id,'q_H2O_cnd2pcp_tnd',nf90_float,dim_lon_lev_time,q_H2O_cnd2pcp_tnd_id)
       rcd=rcd+nf90_def_var(nc_id,'q_H2O_pcp2vpr_tnd',nf90_float,dim_lon_lev_time,q_H2O_pcp2vpr_tnd_id)
       rcd=rcd+nf90_def_var(nc_id,'q_H2O_vpr2pcp_cnv_tnd',nf90_float,dim_lon_lev_time,q_H2O_vpr2pcp_cnv_tnd_id)
       rcd=rcd+nf90_def_var(nc_id,'q_dst_tnd_evp',nf90_float,dim_lon_lev_sz_time,q_dst_tnd_evp_id)
       rcd=rcd+nf90_def_var(nc_id,'q_dst_tnd_evp_ttl',nf90_float,dim_lon_lev_time,q_dst_tnd_evp_ttl_id)
       rcd=rcd+nf90_def_var(nc_id,'q_dst_tnd_ncl',nf90_float,dim_lon_lev_sz_time,q_dst_tnd_ncl_id)
       rcd=rcd+nf90_def_var(nc_id,'q_dst_tnd_pcp',nf90_float,dim_lon_lev_sz_time,q_dst_tnd_pcp_id)
       rcd=rcd+nf90_def_var(nc_id,'q_dst_tnd_pcp_ttl',nf90_float,dim_lon_lev_time,q_dst_tnd_pcp_ttl_id)
       rcd=rcd+nf90_def_var(nc_id,'q_dst_tnd_wet',nf90_float,dim_lon_lev_sz_time,q_dst_tnd_wet_id)
       rcd=rcd+nf90_def_var(nc_id,'spc_xsx_cll_scv',nf90_float,sz_dim_id,spc_xsx_cll_scv_id)
       rcd=rcd+nf90_def_var(nc_id,'spc_xsx_ncl_scv',nf90_float,sz_dim_id,spc_xsx_ncl_scv_id)
       ! Add english text descriptions
       rcd=rcd+nf90_put_att(nc_id,cld_frc_cnv_id,'long_name','Convective cloud fraction')
       rcd=rcd+nf90_put_att(nc_id,cld_frc_id,'long_name','Cloud fraction')
       rcd=rcd+nf90_put_att(nc_id,cld_vlm_id,'long_name','Cloud volume')
       rcd=rcd+nf90_put_att(nc_id,flx_mss_pcp_sfc_id,'long_name','Dust reaching surface in precipitation')
       rcd=rcd+nf90_put_att(nc_id,flx_mss_pcp_sfc_ttl_id,'long_name','Total dust reaching surface in precipitation')
       rcd=rcd+nf90_put_att(nc_id,frc_trc_ist_id,'long_name','Interstitial tracer fraction')
       rcd=rcd+nf90_put_att(nc_id,pcp_flx_sfc_id,'long_name','Total precipitation reaching surface')
       rcd=rcd+nf90_put_att(nc_id,q_H2O_cnd2pcp_tnd_id,'long_name','Condensed H2O to precipitation tendency')
       rcd=rcd+nf90_put_att(nc_id,q_H2O_cnd_id,'long_name','Condensed H2O mixing ratio')
       rcd=rcd+nf90_put_att(nc_id,q_H2O_pcp2vpr_tnd_id,'long_name','H2O precipitation to vapor tendency')
       rcd=rcd+nf90_put_att(nc_id,q_H2O_vpr2pcp_cnv_tnd_id,'long_name','H2O vapor to convective precipitation tendency')
       rcd=rcd+nf90_put_att(nc_id,q_dst_tnd_evp_id,'long_name','Evaporation tendency')
       rcd=rcd+nf90_put_att(nc_id,q_dst_tnd_evp_ttl_id,'long_name','Total evaporation tendency')
       rcd=rcd+nf90_put_att(nc_id,q_dst_tnd_ncl_id,'long_name','Nucleation scavenging tendency')
       rcd=rcd+nf90_put_att(nc_id,q_dst_tnd_pcp_id,'long_name','Scavenging tendency')
       rcd=rcd+nf90_put_att(nc_id,q_dst_tnd_pcp_ttl_id,'long_name','Total scavenging tendency')
       rcd=rcd+nf90_put_att(nc_id,q_dst_tnd_wet_id,'long_name','Wet deposition (evaporation minus scavenging) tendency')
       rcd=rcd+nf90_put_att(nc_id,spc_xsx_cll_scv_id,'long_name','Specific cross section for collision scavenging')
       rcd=rcd+nf90_put_att(nc_id,spc_xsx_ncl_scv_id,'long_name','Specific cross section for nucleation scavenging')
       ! Add units
       rcd=rcd+nf90_put_att(nc_id,cld_frc_cnv_id,'units','fraction')
       rcd=rcd+nf90_put_att(nc_id,cld_frc_id,'units','fraction')
       rcd=rcd+nf90_put_att(nc_id,cld_vlm_id,'units','fraction')
       rcd=rcd+nf90_put_att(nc_id,flx_mss_pcp_sfc_id,'units','kilogram meter-2 second-1')
       rcd=rcd+nf90_put_att(nc_id,flx_mss_pcp_sfc_ttl_id,'units','kilogram meter-2 second-1')
       rcd=rcd+nf90_put_att(nc_id,frc_trc_ist_id,'units','fraction')
       rcd=rcd+nf90_put_att(nc_id,pcp_flx_sfc_id,'units','kilogram meter-2 second-1')
       rcd=rcd+nf90_put_att(nc_id,q_H2O_cnd2pcp_tnd_id,'units','kilogram kilogram-1 second-1')
       rcd=rcd+nf90_put_att(nc_id,q_H2O_cnd_id,'units','kilogram kilogram-1')
       rcd=rcd+nf90_put_att(nc_id,q_H2O_pcp2vpr_tnd_id,'units','kilogram kilogram-1 second-1')
       rcd=rcd+nf90_put_att(nc_id,q_H2O_vpr2pcp_cnv_tnd_id,'units','kilogram kilogram-1 second-1')
       rcd=rcd+nf90_put_att(nc_id,q_dst_tnd_evp_id,'units','kilogram kilogram-1 second-1')
       rcd=rcd+nf90_put_att(nc_id,q_dst_tnd_evp_ttl_id,'units','kilogram kilogram-1 second-1')
       rcd=rcd+nf90_put_att(nc_id,q_dst_tnd_ncl_id,'units','kilogram kilogram-1 second-1')
       rcd=rcd+nf90_put_att(nc_id,q_dst_tnd_pcp_id,'units','kilogram kilogram-1 second-1')
       rcd=rcd+nf90_put_att(nc_id,q_dst_tnd_pcp_ttl_id,'units','kilogram kilogram-1 second-1')
       rcd=rcd+nf90_put_att(nc_id,q_dst_tnd_wet_id,'units','kilogram kilogram-1 second-1')
       rcd=rcd+nf90_put_att(nc_id,spc_xsx_cll_scv_id,'units','meter2 kilogram-1')
       rcd=rcd+nf90_put_att(nc_id,spc_xsx_ncl_scv_id,'units','meter2 kilogram-1')
    else                      ! endif nstep == 1
       ! Get variable IDs
       rcd=nf90_wrp_inq_varid(nc_id,'cld_frc',cld_frc_id)
       rcd=nf90_wrp_inq_varid(nc_id,'cld_frc_cnv',cld_frc_cnv_id)
       rcd=nf90_wrp_inq_varid(nc_id,'cld_vlm',cld_vlm_id)
       rcd=nf90_wrp_inq_varid(nc_id,'flx_mss_pcp_sfc',flx_mss_pcp_sfc_id)
       rcd=nf90_wrp_inq_varid(nc_id,'flx_mss_pcp_sfc_ttl',flx_mss_pcp_sfc_ttl_id)
       rcd=nf90_wrp_inq_varid(nc_id,'frc_trc_ist',frc_trc_ist_id)
       rcd=nf90_wrp_inq_varid(nc_id,'pcp_flx_sfc',pcp_flx_sfc_id)
       rcd=nf90_wrp_inq_varid(nc_id,'q_H2O_cnd',q_H2O_cnd_id)
       rcd=nf90_wrp_inq_varid(nc_id,'q_H2O_cnd2pcp_tnd',q_H2O_cnd2pcp_tnd_id)
       rcd=nf90_wrp_inq_varid(nc_id,'q_H2O_pcp2vpr_tnd',q_H2O_pcp2vpr_tnd_id)
       rcd=nf90_wrp_inq_varid(nc_id,'q_H2O_vpr2pcp_cnv_tnd',q_H2O_vpr2pcp_cnv_tnd_id)
       rcd=nf90_wrp_inq_varid(nc_id,'q_dst_tnd_evp',q_dst_tnd_evp_id)
       rcd=nf90_wrp_inq_varid(nc_id,'q_dst_tnd_evp_ttl',q_dst_tnd_evp_ttl_id)
       rcd=nf90_wrp_inq_varid(nc_id,'q_dst_tnd_ncl',q_dst_tnd_ncl_id)
       rcd=nf90_wrp_inq_varid(nc_id,'q_dst_tnd_pcp',q_dst_tnd_pcp_id)
       rcd=nf90_wrp_inq_varid(nc_id,'q_dst_tnd_pcp_ttl',q_dst_tnd_pcp_ttl_id)
       rcd=nf90_wrp_inq_varid(nc_id,'q_dst_tnd_wet',q_dst_tnd_wet_id)
    endif                     ! endif nstep /= 1
    ! End define mode
    rcd=rcd+nf90_enddef(nc_id)
    ! Write data
    if (nstep == 1) then
       rcd=rcd+nf90_put_var(nc_id,spc_xsx_ncl_scv_id,spc_xsx_ncl_scv)
       rcd=rcd+nf90_put_var(nc_id,spc_xsx_cll_scv_id,spc_xsx_cll_scv)
    endif                     ! endif nstep == 1
    rcd=rcd+nf90_put_var(nc_id,cld_frc_cnv_id,cld_frc_cnv,start=srt_lon_lev_time,count=cnt_lon_lev_time)
    rcd=rcd+nf90_put_var(nc_id,cld_frc_id,cld_frc,start=srt_lon_lev_time,count=cnt_lon_lev_time)
    rcd=rcd+nf90_put_var(nc_id,cld_vlm_id,cld_vlm,start=srt_lon_lev_time,count=cnt_lon_lev_time)
    rcd=rcd+nf90_put_var(nc_id,flx_mss_pcp_sfc_id,flx_mss_pcp_sfc,start=srt_lon_sz_time,count=cnt_lon_sz_time)
    rcd=rcd+nf90_put_var(nc_id,flx_mss_pcp_sfc_ttl_id,flx_mss_pcp_sfc_ttl,start=srt_lon_time,count=cnt_lon_time)
    rcd=rcd+nf90_put_var(nc_id,frc_trc_ist_id,frc_trc_ist,start=srt_lon_lev_sz_time,count=cnt_lon_lev_sz_time)
    rcd=rcd+nf90_put_var(nc_id,pcp_flx_sfc_id,pcp_flx_sfc,start=srt_lon_time,count=cnt_lon_time)
    rcd=rcd+nf90_put_var(nc_id,q_H2O_cnd2pcp_tnd_id,q_H2O_cnd2pcp_tnd,start=srt_lon_lev_time,count=cnt_lon_lev_time)
    rcd=rcd+nf90_put_var(nc_id,q_H2O_cnd_id,q_H2O_cnd,start=srt_lon_lev_time,count=cnt_lon_lev_time)
    rcd=rcd+nf90_put_var(nc_id,q_H2O_pcp2vpr_tnd_id,q_H2O_pcp2vpr_tnd,start=srt_lon_lev_time,count=cnt_lon_lev_time)
    rcd=rcd+nf90_put_var(nc_id,q_H2O_vpr2pcp_cnv_tnd_id,q_H2O_vpr2pcp_cnv_tnd,start=srt_lon_lev_time,count=cnt_lon_lev_time)
    rcd=rcd+nf90_put_var(nc_id,q_dst_tnd_evp_id,q_dst_tnd_evp,start=srt_lon_lev_sz_time,count=cnt_lon_lev_sz_time)
    rcd=rcd+nf90_put_var(nc_id,q_dst_tnd_evp_ttl_id,q_dst_tnd_evp_ttl,start=srt_lon_lev_time,count=cnt_lon_lev_time)
    rcd=rcd+nf90_put_var(nc_id,q_dst_tnd_ncl_id,q_dst_tnd_ncl,start=srt_lon_lev_sz_time,count=cnt_lon_lev_sz_time)
    rcd=rcd+nf90_put_var(nc_id,q_dst_tnd_pcp_id,q_dst_tnd_pcp,start=srt_lon_lev_sz_time,count=cnt_lon_lev_sz_time)
    rcd=rcd+nf90_put_var(nc_id,q_dst_tnd_pcp_ttl_id,q_dst_tnd_pcp_ttl,start=srt_lon_lev_time,count=cnt_lon_lev_time)
    rcd=rcd+nf90_put_var(nc_id,q_dst_tnd_wet_id,q_dst_tnd_wet,start=srt_lon_lev_sz_time,count=cnt_lon_lev_sz_time)
    ! Close output file
    rcd=nf90_wrp_close(nc_id,fl_out,'',sbr_nm=sbr_nm)
    if (nstep == 1) then
       write (6,'(a,a45,a)') prg_nm(1:ftn_strlen(prg_nm)), &
            ': Initialized wet deposition data archive in ',fl_out(1:ftn_strlen(fl_out))
    endif                     ! endif
    return 
  end subroutine dpsscv2nc                       ! end dpsscv2nc()
  
#if 0
  ! Calculation of collision efficiency E discussed on SeP97 p. 1019, DaH76, NGD94
  ! All modern sources seem based on work of W. G. N. Slinn: Sli82, Sli84
  real(r8),parameter::dmt_pcp=1.0e-3 ! [m] Raindrop diameter, Option dmt_pcp
  real(r8),parameter::dns_H2O_lqd_std=1000.0 ! [kg m-3] Density of liquid water
  integer,parameter::pcp_nbr=1 ! [nbr] Number of raindrop sizes
  real(r8) flx_hgt_pcp(plond,plev) ! [m s-1] Precipitation height flux, Option flx_hgt_pcp
  real(r8) stk_crc_pcp          ! I [frc] Correction to Stokes settling velocity
  real(r8) vlc_grv(plond,plev,pcp_nbr) ! [m s-1] Raindrop Settling velocity
  ! Main Code
  
  do m=1,pcp_nbr
     dns_pcp(m)=dns_H2O_lqd_std ! [kg m-3] Density of precipitation
  end do                    ! end loop over pcp
  
  ! Corrections from Stokes' settling velocities to terminal velocities
  call stk_crc_get( &
       dmt_pcp_vwr,         & ! I [m] Raindrop Mass-weighted mean diameter resolved
       dns_pcp,             & ! I [kg m-3] Precipitation density
       stk_crc_pcp,         & ! O [frc] Correction to Stokes settling velocity of raindrops
       pcp_nbr)             ! [nbr] Number of raindrop sizes
  
  ! Gravitational settling velocity
  call vlc_grv_get( &
       dmt_pcp_vwr,         & ! I [m] Raindrop Mass weighted diameter resolved
       dns_pcp,             & ! I [kg m-3] Precipitation density
       prs_mdp,             & ! I [Pa] Pressure
       stk_crc_pcp,         & ! I [frc] Correction to Stokes settling velocity of raindrops
       tpt_mdp,             & ! I [K] Temperature
       vlc_grv_pcp)         ! O [m s-1] Raindrop Settling velocity
  
  real(r8) dmt_rat              ! [frc] Ratio of collectee to collector sizes
  real(r8) cll_fsh(dst_nbr)     ! [frc] Collision efficiency
  real(r8) clc_fsh(dst_nbr)     ! [frc] Collection efficiency
  real(r8) stc_fsh(dst_nbr)     ! [frc] Sticking (coalescence) efficiency
  ! The viscosity ratio use in PrK98 replaces the ratio of raindrop internal circulation to raindrop fall speed used in Sli82
  ! Results for the latter are not given in Sli82, but are discussed extensively in PrK98 Section 10.3.1 pp. 386--393
  const real(r8) vsc_rat(vsc_dyn_H2O/vsc_dyn_atm) ! [frc] Ratio of liquid H2O to atmospheric dynamic viscosity
  assert(vsc_rat > 20.0_r8 && vsc_rat < 100.0_r8) ! Based on PrK98 p. 387 discussion
  const real(r8) dns_rat(dns_H2O_lqd_std/dns_aer) ! [frc] Ratio of liquid H2O to aerosol density
  ! Parameterization for critical Stokes number is discussed in Sli82 p. 332
  ! Formula comes from fit to data of Beard and Grover (1974)
  const real(r8) stk_nbr_crt=(1.2_r8+(1.0_r8/12.0_r8)*log(1.0+ryn_nbr_pcp))/(1.0_r8+log(1.0_r8+ryn_nbr_pcp)) ! [frc] Critical Stokes number Sli82 p. 329 and p. 331, SeP97 p. 1019 (20.57)
  real(r8) stk_nbr_rlt(dst_nbr) ! [frc] Stokes number of relative flow
  real(r8) tau_rlx(dst_nbr)     ! [s] Relaxation timescale
  real(r8) cll_fsh_brn_dff(dst_nbr) ! [frc] Collision efficiency of Brownian diffusion
  real(r8) cll_fsh_ntc(dst_nbr) ! [frc] Collision efficiency of interception
  real(r8) cll_fsh_mpc(dst_nbr) ! [frc] Collision efficiency of impaction
  real(r8) mpc_fct              ! [frc] Factor in impaction contribution to collision efficiency
  
  for(idx=0;idx<sz_nbr;idx++){
  c   Relaxation timescale is approximate time (truly valid for Stokes flow only) for particle to reach terminal velocity from standing start
  tau_rlx(idx)=mss(idx)*slp_crc(idx)/(3.0_r8*M_PI*vsc_dyn_atm*dmt_ctr(idx)) ! [s] Relaxation timescale Sep97 p. 465 (8.38) (validate against Table 8.4)
  stk_nbr_rlt(idx)=2.0_r8*tau_rlx(idx)*fabs((vlc_pcp-vlc_grv(idx)))/dmt_ctr(idx) ! [frc] Stokes number of relative flow SeP97 p. 1019
  
  c   Collection due to Brownian Diffusion accounts for particles "random walking" across streamlines
  c   Brownian diffusion is primarily important for particles D < 2 um
  cll_fsh_brn_dff(idx)=4.0_r8/(ryn_nbr_pcp*shm_nbr(idx)) ! [frc] Collision efficiency of Brownian diffusion SeP97 p. 1019 (20.56) Sli82 p. 324 (45), NGD94 p. 2336 (10)
  c   The last term of the Brownian diffusion expression attempts to account for the internal circulation of raindrops
  cll_fsh_brn_dff(idx)*=1.0_r8+0.4_r8*sqrt(ryn_nbr_pcp)*pow(shm_nbr(idx),1.0_r8/3.0_r8)+0.16_r8*sqrt(ryn_nbr_pcp*shm_nbr(idx)) ! [frc] Collision efficiency of Brownian diffusion SeP97 p. 1019 (20.56), Sli82 p. 324 (45) and p. 331 caption of Figure 11, NGD94 p. 2336 (10)
  
  c   Collection due to impaction accounts for particles which are unable to follow the streamlines of fluid flow around an approaching raindrop
  c   Due to their large size (Stokes number) and the sharpness of the streamlines, these particles are unable to move out of the way of the raindrops so collection occurs
  c   Contribution from impaction vanishes unless St > St* Sli82 p. 332
  c   Impaction is primarily important for particles D > 5 um
  cll_fsh_mpc(idx)=0.0_r8      ! [frc] Collision efficiency of impaction SeP97 p. 1019 (20.56)
  if (stk_nbr(idx) >= stk_nbr_crt){
  mpc_fct=(stk_nbr_rlt(idx)-stk_nbr_crt)/(stk_nbr_rlt(idx)-stk_nbr_crt+2.0_r8/3.0_r8) ! [frc] Factor in impaction contribution to collision efficiency SeP97 p. 1019 (20.56) Sli82 p. 331, NGD94 p. 2336 (9)
  cll_fsh_mpc(idx)=sqrt(dns_rat)*pow(mpc_fct,1.5_r8) ! [frc] Collision efficiency of impaction SeP97 p. 1019 (20.56), NGD94 p. 2336 (9)
  } // endif
  
  c   Collection due to interception accounts for particles which follow the streamlines of flow around an approaching raindrop
  c   When the particle radius is larger than the distance of the streamline to the raindrop, interception occurs
  c   Thus interception is strictly due to particle size, not mass
  c   Interception is primarily important for particles 2 < D < 5 um
  c   fxm: 20000527 interception component is too large, can grow to well over unity
  c   Ratio of sizes depends on relative size of aerosol and precipitation
  c   Smaller particle ("collectee") must be in numerator
  if(dmt_ctr(idx) < dmt_pcp) dmt_rat=dmt_ctr(idx)/dmt_pcp; else dmt_rat=dmt_pcp/dmt_ctr(idx) ! [frc] Ratio of collectee to collector sizes
  cll_fsh_ntc(idx)=4.0_r8*dmt_rat*((1.0_r8/vsc_rat)+dmt_rat*(1.0_r8+2.0_r8*sqrt(ryn_nbr_pcp))) ! [frc] Collision efficiency of interception SeP97 p. 1019 (20.56), NGD94 p. 2336 (8)
  c   If interception efficiency is too large limit it to Fuchs (1964) result for flow about a sphere
  if(cll_fsh_ntc(idx) > 1.0_r8) cll_fsh_ntc(idx)=3.0_r8*dmt_rat ! [frc] Collision efficiency of interception Sli82 p. 325 (47)
  c   If interception efficiency is still too large then limit it to pure geometric result
  if(cll_fsh_ntc(idx) > 1.0_r8) cll_fsh_ntc(idx)=dmt_rat*dmt_rat ! [frc] Collision efficiency of interception Sli82 p. 325 (46)
  c   fxm: Bogus hack so that collision efficiency will not exceed unity
  c   Interception efficiency seems the most logical to adjust
  if(cll_fsh_brn_dff(idx)+cll_fsh_ntc(idx)+cll_fsh_mpc(idx) > 1.0_r8) cll_fsh_ntc(idx)=1.0_r8-(cll_fsh_brn_dff(idx)+cll_fsh_mpc(idx)) ! [frc] Collision efficiency of interception SeP97 p. 1019 (20.56)
  
  c   Sum diffusion, interception, and impaction contributions to total collision efficiency
  cll_fsh(idx)=cll_fsh_brn_dff(idx)+cll_fsh_ntc(idx)+cll_fsh_mpc(idx) ! [frc] Collision efficiency SeP97 p. 1019 (20.56)
  assert(cll_fsh(idx) <= 1.0_r8);
  
  c   Sticking efficiency is probably unity for D < 20 um Sli82 p. 330
  stc_fsh(idx)=1.0_r8          ! [frc] Sticking (coalescence) efficiency
  clc_fsh(idx)=stc_fsh(idx)*cll_fsh(idx) ! [frc] Collection efficiency
  } // end loop over sz
  
  real(r8) scv_cff(dst_nbr)     ! [s-1] Scavenging coefficient
  real(r8) scv_cff_mss_avg(0.0_r8) ! [s-1] Mass mean scavenging coefficient of aerosol
  real(r8) scv_cff_nbr_avg(0.0_r8) ! [s-1] Number mean scavenging coefficient of aerosol
  ! fxm: Fill in scavenging coefficient definitions
  const real(r8) xsx_pcp(M_PI*dmt_pcp*dmt_pcp*0.25_r8) ! [m2] Cross-sectional area of precipitation
  for(idx=0;idx<sz_nbr;idx++){
  scv_cff(idx)=xsx_pcp*vlc_pcp*clc_fsh(idx)*cnc_nbr_pcp ! [s-1] Scavenging coefficient SeP97 p. 1021 (20.53, 20.58)
  scv_cff_mss_avg+=scv_cff(idx)*dst_mss(idx) ! [s-1] Mass mean scavenging coefficient of aerosol SeP97 p. 1022 (20.63)
  scv_cff_nbr_avg+=clc_fsh(idx)*cnc(idx) ! [s-1] Number mean scavenging coefficient of aerosol SeP97 p. 1022 (20.63)
  } // end loop over sz
  scv_cff_mss_avg/=mss_rsl  ! [s-1] Mass mean scavenging scavenging coefficient
  scv_cff_nbr_avg/=cnc_nbr_rsl ! [s-1] Number mean scavenging scavenging coefficient
#endif /* not FALSE */
  
  subroutine clc_fsh_get( &
       dmt_aer,             & ! I 
       dns_aer,             & ! I
       clc_fsh)             ! O
    ! Purpose: Compute factor which corrects Stokes settling velocity to actual
    ! settling velocity when Reynolds' number is greater than 0.1
    ! clc_fsh_get() is called by dst_psd_ini()
    ! This implementation isolates the size-dependent sensitivity of the settling velocity 
    use dstgrd ! [mdl] Dust grid sizes
    use dstcst ! [mdl] Physical constants for dust routines
    implicit none
    ! Parameters
    ! Apparently Boltzmann's constant is not set anywhere by CCM or MATCH
    real(r8),parameter::cst_Boltzmann=1.38063e-23 ! [J K-1] Boltzmann's constant
    real(r8),parameter::prs_mdp=100000.0 ! [Pa] Pressure 
    real(r8),parameter::tpt_mdp=295.0 ! [K] Temperature
    real(r8),parameter::tpt_vrt=295.0 ! [K] Virtual temperature
    ! Input
    real(r8) dmt_aer(dst_nbr)     ! [m] Particle diameter
    real(r8) dns_aer(dst_nbr)     ! [kg m-3] Particle density
    ! Output
    real(r8) clc_fsh(dst_nbr)     ! [frc] Correction to Stokes settling velocity
    ! Local
    real(r8) pi       ! [frc] 3
    integer itr_idx           ! [idx] Counting index
    integer m                 ! [idx] Counting index
    real(r8) cff_drg_grv(dst_nbr) ! [frc] Drag coefficient at terminal velocity
    real(r8) dns_mdp              ! [kg m-3] Midlayer density
    real(r8) eps_crr              ! [frc] Current relative accuracy
    real(r8) eps_max              ! [frc] Relative accuracy for convergence
    real(r8) mfp_atm              ! [m] Mean free path of air
    real(r8) ryn_nbr_grv(dst_nbr) ! [frc] Reynolds number at terminal velocity
    real(r8) slp_crc(dst_nbr)     ! [frc] Slip correction factor
    real(r8) vlc_grv(dst_nbr)     ! [m s-1] Settling velocity
    real(r8) vlc_grv_old          ! [m s-1] Previous gravitational settling velocity
    real(r8) vlc_stk(dst_nbr)     ! [m s-1] Stokes settling velocity
    real(r8) vsc_dyn_atm          ! [kg m-1 s-1] Dynamic viscosity of air
    real(r8) vsc_knm_atm          ! [m2 s-1] Kinematic viscosity of atmosphere 
    ! Initialize some variables
    pi=4.0_DBLKIND*atan(1.0_DBLKIND)        ! [frc] 3
    eps_max=1.0e-4_r8            ! [frc] Relative accuracy for convergence
    dns_mdp=prs_mdp/(tpt_vrt*gas_cst_dry_air) ! [kg m-3]
    ! Size-independent thermokinetic properties
    vsc_dyn_atm=1.72e-5_r8*((tpt_mdp/273.0_r8)**1.5_r8)*393.0_r8/(tpt_mdp+120.0_r8) ! [kg m-1 s-1] RoY94  p. 102
    mfp_atm=2.0_r8*vsc_dyn_atm/(prs_mdp*sqrt(8.0_r8*mmw_dry_air/(pi*gas_cst_unv*tpt_mdp))) ! [m] Mean free path of air SeP97 p. 455
    vsc_knm_atm=vsc_dyn_atm/dns_mdp ! [m2 s-1] Kinematic viscosity of air
    
    do m=1,dst_nbr
       slp_crc(m)=1.0_r8+2.0_r8*mfp_atm*(1.257_r8+0.4_r8*exp(-1.1_r8*dmt_aer(m)/(2.0*mfp_atm)))/dmt_aer(m) ! [frc] Slip correction factor SeP97 p. 464
       vlc_stk(m)=(1.0_r8/18.0_r8)*dmt_aer(m)*dmt_aer(m)*dns_aer(m)*grv_sfc*slp_crc(m)/vsc_dyn_atm ! [m s-1] Stokes settling velocity SeP97 p. 466
    end do                     ! end loop over size
    
    ! For Reynolds number flows Re < 0.1 Stokes' velocity is valid for vlc_grv SeP97 p. 466 (8.42)
    ! For larger Re, inertial effects become important and empirical drag coefficients must be employed
    ! Implicit equation for Re, Cd, and Vt is SeP97 p. 467 (8.44)
    ! Using Stokes' velocity rather than iterative solution with empirical drag coefficient causes 60% errors for D = 200 um SeP97 p. 468
    
    ! Iterative solution for drag coefficient, Reynolds number, and terminal velocity
    do m=1,dst_nbr
       ! Initialize accuracy and counter
       eps_crr=eps_max+1.0_r8    ! [frc] Current relative accuracy
       itr_idx=0              ! [idx] Counting index
       ! Initial guess for vlc_grv is exact for Re < 0.1
       vlc_grv(m)=vlc_stk(m)  ! [m s-1]
       do while(eps_crr > eps_max)
          ! Save terminal velocity for convergence test
          vlc_grv_old=vlc_grv(m) ! [m s-1] 
          ryn_nbr_grv(m)=vlc_grv(m)*dmt_aer(m)/vsc_knm_atm ! [frc] SeP97 p. 460
          ! Update drag coefficient based on new Reynolds number
          if (ryn_nbr_grv(m) < 0.1_r8) then
             cff_drg_grv(m)=24.0_r8/ryn_nbr_grv(m) ! Stokes' law Sep97 p. 463 (8.32)
          else if (ryn_nbr_grv(m) < 2.0_r8) then
             cff_drg_grv(m)=(24.0_r8/ryn_nbr_grv(m))* &
                  (1.0_r8+3.0_r8*ryn_nbr_grv(m)/16.0_r8+9.0_r8*ryn_nbr_grv(m)*ryn_nbr_grv(m)*log(2.0_r8*ryn_nbr_grv(m))/160.0_r8) ! Sep97 p. 463 (8.32)
          else if (ryn_nbr_grv(m) < 500.0_r8) then
             cff_drg_grv(m)=(24.0_r8/ryn_nbr_grv(m))*(1.0_r8+0.15_r8*ryn_nbr_grv(m)**0.687_r8) ! Sep97 p. 463 (8.32)
          else if (ryn_nbr_grv(m) < 2.0e5_r8) then
             cff_drg_grv(m)=0.44_r8 ! Sep97 p. 463 (8.32)
          else
             stop 'Reynolds number too large in clc_fsh_get()'
          endif               ! end else
          ! Update terminal velocity based on new Reynolds number and drag coefficient
          vlc_grv(m)=sqrt(4.0_r8*grv_sfc*dmt_aer(m)*slp_crc(m)*dns_aer(m)/(3.0_r8*cff_drg_grv(m)*dns_mdp)) ! [m s-1] Terminal velocity SeP97 p. 467 (8.44)
          eps_crr=abs((vlc_grv(m)-vlc_grv_old)/vlc_grv(m)) ! Relative convergence
          if (itr_idx == 12) then
             ! Numerical pingpong may occur when Re = 0.1, 2.0, or 500.0 due to discontinuities in derivative of drag coefficient
             vlc_grv(m)=0.5_r8*(vlc_grv(m)+vlc_grv_old) ! [m s-1]
          endif               ! endif
          if (itr_idx > 20) then
             write (6,'(a)') 'dst: Terminal velocity not converging in clc_fsh_get(), breaking loop...'
             goto 100         ! Jump to next iteration
          endif               ! endif
          itr_idx=itr_idx+1
       end do                 ! end while
100    continue               ! Label to jump to when iteration does not converge
    end do                     ! end loop over size
    
    ! Compute factors to convert Stokes' settling velocities to actual settling velocities
    do m=1,dst_nbr
       clc_fsh(m)=vlc_grv(m)/vlc_stk(m) ! [frc] Correction to Stokes settling velocity
    end do                     ! end loop over size
    return
  end subroutine clc_fsh_get                       ! end clc_fsh_get()
  
end module dstscv
